<prog> ::= program <decl seq> begin <stmt seq> end (1)
<decl seq> ::= <decl> | <decl> <decl seq> (2)
<stmt seq>::= <stmt> | <stmt> <stmt seq> (3)
<decl> ::=int <id list>; (4)
<id list> ::=<id> | <id>, <id list> (5)
<stmt> ::=<assign>|<if>|<loop>|<in>|<out> (6)
<assign> ::=<id> = <exp>; (7)
<if> ::=if <cond> then <stmt seq> end; | if <cond> then <stmt seq> else <stmt seq> end; (8)
<loop> ::=while <cond> loop <stmt seq> end; (9)
<in> ::=read <id list>; (10)
<out> ::=write <id list>; (11)
<cond> ::= <comp>|!<cond> | [<cond> && <cond>] | [<cond> || <cond>] (12)
<comp> ::= (<op> <comp op> <op>) (13)
<exp> ::= <fac>|<fac>+<exp>|<fac>-<exp> (14)
<fac> ::= <op> | <op> * <fac> (15)
<op> ::= <int> | <id> | (<exp>) (16)
<comp op> ::= != | == | < | > | <= | >= (17)
<id> ::= ... as defined by RE ... (18)
<int> ::= ... as defined by RE (only unsigned numbers)(19)